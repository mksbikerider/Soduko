buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.6.RELEASE")
    }
}

repositories {
    mavenCentral()
}

apply plugin: 'org.springframework.boot'
apply plugin: 'war'

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    compile( group: 'io.dropwizard.metrics', name: 'metrics-core', version: '3.1.2' )
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.7'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.7'
    compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.7'
    compile("org.springframework.boot:spring-boot-starter-web:1.5.6.RELEASE"){
        exclude group: 'ch.qos.logback'
    }
    compile("org.springframework.boot:spring-boot-devtools:1.5.6.RELEASE"){
        exclude group: 'ch.qos.logback'
    }
    compile ("org.springframework.boot:spring-boot-starter-jdbc:1.5.6.RELEASE"){
        exclude group: 'ch.qos.logback'
    }
    compile "org.flywaydb:flyway-core:4.2.0"

    runtime "javax.servlet:jstl:1.2"
    runtime "org.hsqldb:hsqldb:2.4.0"

    providedRuntime("org.springframework.boot:spring-boot-starter-tomcat:1.5.6.RELEASE"){
        exclude group: 'ch.qos.logback'
    }
    providedRuntime "org.apache.tomcat.embed:tomcat-embed-jasper:9.0.0.M11"


    testCompile("org.junit.jupiter:junit-jupiter-api:5.0.0-M2")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:5.0.0-M2")
    testCompile("org.junit.platform:junit-platform-runner:1.0.0-M2")
    testCompile('org.springframework:spring-test:4.3.10.RELEASE')

    integrationTestCompile("org.seleniumhq.selenium:selenium-server:3.0.1")
    integrationTestCompile('org.springframework.boot:spring-boot-test:1.5.6.RELEASE'){
        exclude group: 'ch.qos.logback'
    }
    integrationTestRuntime 'org.mockito:mockito-core:1.10.19'
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

springBoot {
    mainClass = 'name.msutherland.config.SpringStart'
}
